pluginManagement {
    repositories {
        mavenLocal()
        maven {
            allowInsecureProtocol = true
            url 'http://dl.bintray.com/umsdk/release'
        }
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}
enableFeaturePreview('VERSION_CATALOGS')
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/releases' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://www.jitpack.io' }
        google()
        mavenCentral()
    }
    versionCatalogs {
        create('androidxLibs') {
            // 别名 group artifact version
            library('swiperefreshlayout','androidx.swiperefreshlayout','swiperefreshlayout').version('1.2.0-alpha01')
            library('palette','androidx.palette','palette').version('1.0.0')
            library('core-ktx', 'androidx.core', 'core-ktx').version('1.7.0')
            library('appcompat', 'androidx.appcompat', 'appcompat').version('1.4.1')
            library('constraintlayout', 'androidx.constraintlayout', 'constraintlayout').version('2.1.3')
            library('material', 'com.google.android.material', 'material').version('1.5.0')
            // navigation
            library('navigation-fragment', 'androidx.navigation', 'navigation-fragment-ktx').version('2.5.3')
            library('navigation-ui', 'androidx.navigation', 'navigation-ui-ktx').version('2.5.3')
            // paging3
            library('ktx-stdlib-common','org.jetbrains.kotlin','kotlin-stdlib-common').version('1.8.0')
            library('paging-runtime', 'androidx.paging', 'paging-runtime').version('3.1.1')
            // viewpager2
            library('viewpager2', 'androidx.viewpager2', 'viewpager2').version('1.0.0')
            // androidX通用依赖
            bundle('androidx', ['viewpager2','swiperefreshlayout','palette','navigation-fragment', 'navigation-ui', 'core-ktx','ktx-stdlib-common', 'appcompat', 'constraintlayout','paging-runtime'])

            // retrofit + okHttp3 = 网络库依赖
            library('glide','com.github.bumptech.glide','glide').version('4.15.0')
            library('glide-compiler','com.github.bumptech.glide','compiler').version('4.15.0')
            library('glide-transformations','jp.wasabeef','glide-transformations').version('4.0.0')
            bundle('glide',['glide','glide-transformations'])

            library('retrofit', 'com.squareup.retrofit2', 'retrofit').version('2.9.0')
            library('converter-gson', 'com.squareup.retrofit2', 'converter-gson').version('2.9.0')
            library('okhttp', 'com.squareup.okhttp3', 'okhttp').version('4.9.1')
            library('logging', 'com.squareup.okhttp3', 'logging-interceptor').version('4.9.1')
            bundle('http', ['retrofit', 'converter-gson', 'okhttp', 'logging'])

            // exoplayer
            library('exoplayer-core','com.google.android.exoplayer','exoplayer-core').version('2.18.5')
            library('exoplayer-ui','com.google.android.exoplayer','exoplayer-ui').version('2.18.5')
            bundle('exoplayer', ['exoplayer-core', 'exoplayer-ui'])

            // qq_open_sdk
            library('qqopensdk','com.tencent.tauth','qqopensdk').version('3.52.0')

            // room
            def room_version='2.5.0'
            library('room-runtime','androidx.room','room-runtime').version(room_version)
            library('room-ktx','androidx.room','room-ktx').version(room_version)
            library('room-compiler','androidx.room','room-compiler').version(room_version)
            bundle('room',['room-runtime','room-ktx'])

            //workmanager
            library ('work','androidx.work','work-runtime').version('2.8.0')

            // aliyun oss
            library('osssdk','com.aliyun.dpa','oss-android-sdk').version('2.9.11')

            // camerax
            def camerax_version="1.3.0-alpha04"
            library('camera-core','androidx.camera','camera-core').version(camerax_version)
            library('camera-camera2','androidx.camera','camera-camera2').version(camerax_version)
            library('camera-lifecycle','androidx.camera','camera-lifecycle').version(camerax_version)
            library('camera-view','androidx.camera','camera-view').version(camerax_version)
            library('camera-extensions','androidx.camera','camera-extensions').version(camerax_version)
            bundle('camerax',['camera-core','camera-camera2','camera-lifecycle','camera-view','camera-extensions'])

            // gesture imageview
            library('photoView','com.github.chrisbanes','PhotoView').version('2.3.0')

            // nav-plugin
            library('asm', 'org.ow2.asm', 'asm').version('9.2')
            library('asm-tree', 'org.ow2.asm', 'asm-tree').version('9.2')
            library('commons-io', 'commons-io', 'commons-io').version('2.6')
            library('commons-codec', 'commons-codec', 'commons-codec').version('1.11')
            library('kotlinpoet', 'com.squareup', 'kotlinpoet').version('1.2.0')
            library('agp', 'com.android.tools.build', 'gradle').version('7.4.1')
            bundle('nav-plugin', ['asm', 'asm-tree', 'commons-io','commons-codec', 'kotlinpoet', 'agp'])


        }

        create('googleLibs') {
            library('material', 'com.google.android', 'material').version('1.5.0')
        }

        create('composeLibs'){
            // Bom与Compose依赖库版本对应关系
            // https://developer.android.com/jetpack/compose/bom/bom-mapping?hl=zh-cn
            // 目前使用的是Bom:2023.01.00

            library('bom','androidx.compose','compose-bom').version('2023.01.00')

            library('material', 'androidx.compose.material', 'material').withoutVersion()
            library('ui', 'androidx.compose.ui', 'ui').withoutVersion()
            library('tooling','androidx.compose.ui','ui-tooling').withoutVersion()
            library('preview', 'androidx.compose.ui', 'ui-tooling-preview').withoutVersion()
            library('icons', 'androidx.compose.material', 'material-icons-core').withoutVersion()
            library('icons-ext', 'androidx.compose.material', 'material-icons-extended').withoutVersion()
            // foundation使用了1.4.0-beta02，是因为该版本中LazyVerticalStaggeredGrid
            // 才支持自定义跨列(spanCount)的能力
            library('foundation', 'androidx.compose.foundation', 'foundation').version('1.4.0-beta02')
            library('activity-compose', 'androidx.activity', 'activity-compose').version('1.6.1')
            library('viewmodel-compose', 'androidx.lifecycle', 'lifecycle-viewmodel-compose').version('2.5.1')

            // 加载网络图片
            // 普通图片
            library('coil','io.coil-kt','coil-compose').version('2.4.0')
            // gif图片
            library('coil-gif','io.coil-kt','coil-gif').version('2.4.0')
            // svg图片
            library('coil-svg','io.coil-kt','coil-svg').version('2.4.0')
            bundle('compose', ['coil','coil-gif','coil-svg','material', 'ui', 'preview','tooling','icons', 'icons-ext','foundation','activity-compose','viewmodel-compose'])
        }

        /**
         * 在app/build.gradle中
         * compileSdk:buildsdk.versions.compilesdk.get().toInteger()
         */
        create('buildsdk') {
            version('compilesdk', '33')
            version('minisdk', '23')
            version('targetsdk', '33')
        }
        /**
         * 1、只需在app/build.gradle中alias(pluginLibs.plugins.hilt.android)即可
         * 2、对于没有发布到gradlePluginPortal的插件，暂时沿用老的模式，即buildScript { dependencies{  classpath 'xxx.xxx.xxx:1.0.0'}}
         * create('pluginLibs'){
         *    plugin('hilt-android','com.google.dagger.hilt.android').version('2.41')
         * }
         */
    }
}
rootProject.name = "VideoShort"
include ':app'
include ':nav-plugin'
include ':nav-plugin-runtime'
